/* Definition section */
%{
    #include "compiler_hw_common.h"
    #include "y.tab.h"	/* header file generated by bison */
    extern YYSTYPE yylval;

    #define YY_NO_UNPUT
    #define YY_NO_INPUT
    #define XXX printf("not implemented yet!\n")
%}

/* Define regular expression label */
letter [a-zA-Z_]
digit [0-9]
id {letter}+({letter}|{digit})*
inumber {digit}+
fnumber ({digit}*\.{digit}+)

%x CMT
%x STR
%option yylineno

/* Rules section */
%%

"/*"        { BEGIN(CMT); }
<CMT>"*/"   { BEGIN(INITIAL); }
<CMT>\n     {;}
<CMT>.      {;}
"//".*      {;}
"\""        { BEGIN(STR);
                return '"';
            }
<STR>"\""   { BEGIN(INITIAL);
                return '"';
            }
<STR>[^\"]* { yylval.s_val = strdup(yytext);
                return STRING_LIT;
            }
 /* Arithmetic */            
"+"         { return '+'; }
"-"         { return '-'; }
"*"         { return '*'; }
"/"         { return '/'; }
"%"         { return '%'; }
"++"        { return INC; }
"--"        { return DEC; }

 /* Relational */
">"         { return '>'; }
"<"         { return '<'; }
">="        { return GEQ; }
"<="        { return LEQ ; }
"=="        { return EQL; }
"!="        { return NEQ ; }

 /* Assignment */
"="         { return '='; }
"+="        { return ADD_ASSIGN; }
"-="        { return SUB_ASSIGN; }
"*="        { return MUL_ASSIGN; }
"/="        { return QUO_ASSIGN; }
"%="        { return REM_ASSIGN; }

 /* Logical */
"&&"        { return LAND; }
"||"        { return LOR; }
"!"         { return '!'; }

 /* Delimiters */
"("         { return '('; }
")"         { return ')'; }
"["         { return '['; }
"]"         { return ']'; }
"{"         { return '{'; }
"}"         { return '}'; }

";"         { return ';'; }
":"         { return ':'; }
","         { return ','; }
"\n"        { return NEWLINE; }
 /* Print */
"print"     { return PRINT; }
"println"   { return PRINTLN; }

 /* Condition Loop */
"if"        { return IF; }
"else"      { return ELSE; }
"for"       { return FOR; }
"switch"    { return SWITCH; }
"case"      { return CASE; }

"var"       { return VAR; }
"package"   { return PACKAGE; }
"default"   { return DEFAULT; }
"func"      { return FUNC; }
"return"    { return RETURN; }

 /* type */
"int32"     { return INT; }
"float32"   { return FLOAT;}
"string"    { return STRING; }
"bool"      { return BOOL; }
"true"      { yylval.s_val = "TRUE 1"; return BOOL_LIT; }
"false"     { yylval.s_val = "FALSE 0"; return BOOL_LIT; }

{inumber}   { yylval.s_val = strdup(yytext);
                return INT_LIT;
            }
{fnumber}   { yylval.s_val = strdup(yytext);
                return FLOAT_LIT; }
{id}        { yylval.s_val = strdup(yytext);
                return IDENT; }

<<EOF>>     { static int once = 0;
                if (once++) {
                    yyterminate();
                }
                yylineno++;
                return NEWLINE;
            }
[ \t]+      {;}
.           {;}
%%
/*  C Code section */
int yywrap(void)
{
    return 1;
}